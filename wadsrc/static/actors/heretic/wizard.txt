
// Wizard --------------------------------------------------------

ACTOR Wizard
{
	Health 180
	Radius 16
	Height 68
	Mass 100
	Speed 12
	Painchance 64
	Monster
	+FLOAT
	+NOGRAVITY
	+DONTOVERLAP
	SeeSound "wizard/sight"
	AttackSound "wizard/attack"
	PainSound "wizard/pain"
	DeathSound "wizard/death"
	ActiveSound "wizard/active"
	Obituary "$OB_WIZARD"
	HitObituary "$OB_WIZARDHIT"
	DropItem "BlasterAmmo", 84, 10
	DropItem "ArtiTomeOfPower", 4, 0

	action native A_GhostOff ();
	action native A_WizAtk1 ();
	action native A_WizAtk2 ();
	action native A_WizAtk3 ();

	States
	{
	Spawn:
		WZRD AB 10 A_Look
		Loop
	See:
		WZRD A 3 A_Chase
		WZRD A 4 A_Chase
		WZRD A 3 A_Chase
		WZRD A 4 A_Chase
		WZRD B 3 A_Chase
		WZRD B 4 A_Chase
		WZRD B 3 A_Chase
		WZRD B 4 A_Chase
		Loop
	Missile:
		WZRD C 4 A_WizAtk1
		WZRD C 4 A_WizAtk2
		WZRD C 4 A_WizAtk1
		WZRD C 4 A_WizAtk2
		WZRD C 4 A_WizAtk1
		WZRD C 4 A_WizAtk2
		WZRD C 4 A_WizAtk1
		WZRD C 4 A_WizAtk2
		WZRD D 12 A_WizAtk3
		Goto See
	Pain:
		WZRD E 3 A_GhostOff
		WZRD E 3 A_Pain
		Goto See
	Death:
		WZRD F 6 A_GhostOff
		WZRD G 6 A_Scream
		WZRD HI 6
		WZRD J 6 A_NoBlocking
		WZRD KL 6
		WZRD M -1 A_SetFloorClip
		Stop
	}
}

// Projectile --------------------------------------------------------

ACTOR WizardFX1
{
	Radius 10
	Height 6
	Speed 18
	FastSpeed 24
	Damage 3
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	RenderStyle Add
	States
	{
	Spawn:
		FX11 AB 6 BRIGHT
		Loop
	Death:
		FX11 CDEFG 5 BRIGHT
		Stop
	}
}


