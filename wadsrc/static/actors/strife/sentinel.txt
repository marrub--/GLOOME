
// Sentinel -----------------------------------------------------------------

ACTOR Sentinel
{
	Health 100
	Painchance 255
	Speed 7
	Radius 23
	Height 53
	Mass 300
	Monster
	+SPAWNCEILING
	+NOGRAVITY
	+DROPOFF
	+NOBLOOD
	+NOBLOCKMONST
	+INCOMBAT
	+MISSILEMORE
	+LOOKALLAROUND
	+NEVERRESPAWN
	MinMissileChance 150
	SeeSound "sentinel/sight"
	DeathSound "sentinel/death"
	ActiveSound "sentinel/active"
	Obituary "$OB_SENTINEL"

	action native A_SentinelAttack ();

	States
	{
	Spawn:
		SEWR A 10 A_Look
		Loop
	See:
		SEWR A 6 A_SentinelBob
		SEWR A 6 A_Chase
		Loop
	Missile:
		SEWR B 4 A_FaceTarget
		SEWR C 8 Bright A_SentinelAttack
		SEWR C 4 Bright A_SentinelRefire
		Goto Missile+1
	Pain:
		SEWR D 5 A_Pain
		Goto Missile+2
	Death:
		SEWR D 7 A_Fall
		SEWR E 8 Bright A_TossGib
		SEWR F 5 Bright A_Scream
		SEWR GH 4 Bright A_TossGib
		SEWR I 4
		SEWR J 5
		Stop
	}
}

// Sentinel FX 1 ------------------------------------------------------------

ACTOR SentinelFX1
{
	Speed 40
	Radius 10
	Height 8
	Damage 0
	DamageType Disintegrate
	Projectile
	+STRIFEDAMAGE
	MaxStepHeight 4
	RenderStyle Add
	States
	{
	Spawn:
		SHT1 AB 4
		Loop
	Death:
		POW1 J 4
		Stop
	}
}

// Sentinel FX 2 ------------------------------------------------------------

ACTOR SentinelFX2 : SentinelFX1
{
	SeeSound "sentinel/plasma"
	Damage 1
	States
	{
	Death:
		POW1 FGHI 4
		Goto Super::Death
	}
}

