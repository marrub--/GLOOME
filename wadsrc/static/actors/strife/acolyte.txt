
// Base class for the acolytes ----------------------------------------------

ACTOR Acolyte : StrifeHumanoid
{
	Health 70
	PainChance 150
	Speed 7
	Radius 24
	Height 64
	Mass 400
	Monster
	+SEESDAGGERS
	+NOSPLASHALERT
	+FLOORCLIP
	+NEVERRESPAWN
	MinMissileChance 150
	Tag "$TAG_ACOLYTE"
	SeeSound "acolyte/sight"
	PainSound "acolyte/pain"
	AttackSound "acolyte/rifle"
	DeathSound "acolyte/death"
	ActiveSound "acolyte/active"
	Obituary "$OB_ACOLYTE"

	action native A_BeShadowyFoe ();
	action native A_AcolyteBits ();
	action native A_AcolyteDie ();

	States
	{
	Spawn:
		AGRD A 5 A_Look2
		Wait
		AGRD B 8 A_ClearShadow
		Loop
		AGRD D 8
		Loop
		AGRD ABCDABCD 5 A_Wander
		Loop
	See:
		AGRD A 6 Fast Slow A_AcolyteBits
		AGRD BCD 6 Fast Slow A_Chase
		Loop
	Missile:
		AGRD E 8 Fast Slow A_FaceTarget
		AGRD FE 4 Fast Slow A_ShootGun
		AGRD F 6 Fast Slow A_ShootGun
		Goto See
	Pain:
		AGRD O 8 Fast Slow A_Pain
		Goto See
	Death:
		AGRD G 4
		AGRD H 4 A_Scream
		AGRD I 4
		AGRD J 3
		AGRD K 3 A_NoBlocking
		AGRD L 3
		AGRD M 3 A_AcolyteDie
		AGRD N -1
		Stop
	XDeath:
		GIBS A 5 A_NoBlocking
		GIBS BC 5 A_TossGib
		GIBS D 4 A_TossGib
		GIBS E 4 A_XScream
		GIBS F 4 A_TossGib
		GIBS GH 4
		GIBS I 5
		GIBS J 5 A_AcolyteDie
		GIBS K 5
		GIBS L 1400
		Stop
	}
}
	

// Acolyte 1 ----------------------------------------------------------------

ACTOR AcolyteTan : Acolyte
{
	+MISSILEMORE +MISSILEEVENMORE
	DropItem "ClipOfBullets"
}

// Acolyte 2 ----------------------------------------------------------------

ACTOR AcolyteRed : Acolyte
{
	+MISSILEMORE +MISSILEEVENMORE
	Translation 0
}

// Acolyte 3 ----------------------------------------------------------------

ACTOR AcolyteRust : Acolyte
{
	+MISSILEMORE +MISSILEEVENMORE
	Translation 1
}

// Acolyte 4 ----------------------------------------------------------------

ACTOR AcolyteGray : Acolyte
{
	+MISSILEMORE +MISSILEEVENMORE
	Translation 2
}

// Acolyte 5 ----------------------------------------------------------------

ACTOR AcolyteDGreen : Acolyte
{
	+MISSILEMORE +MISSILEEVENMORE
	Translation 3
}

// Acolyte 6 ----------------------------------------------------------------

ACTOR AcolyteGold : Acolyte
{
	+MISSILEMORE +MISSILEEVENMORE
	Translation 4
}

// Acolyte 7 ----------------------------------------------------------------

ACTOR AcolyteLGreen : Acolyte
{
	Health 60
	Translation 5
}

// Acolyte 8 ----------------------------------------------------------------

ACTOR AcolyteBlue : Acolyte
{
	Health 60
	Translation 6
}

// Shadow Acolyte -----------------------------------------------------------

ACTOR AcolyteShadow : Acolyte
{
	+MISSILEMORE
	DropItem "ClipOfBullets"
	States
	{
	See:
		AGRD A 6 A_BeShadowyFoe
		Goto Super::See+1
	Pain:
		AGRD O 0 Fast Slow A_SetShadow
		AGRD O 8 Fast Slow A_Pain
		Goto See
	}
}

// Some guy turning into an acolyte -----------------------------------------

ACTOR AcolyteToBe : Acolyte
{
	Health 61
	Radius 20
	Height 56
	DeathSound "becoming/death"
	-COUNTKILL
	-ISMONSTER

	action native A_HideDecepticon ();

	States
	{
	Spawn:
		ARMR A -1
		Stop
	Pain:
		ARMR A -1 A_HideDecepticon
		Stop
	Death:
		Goto XDeath
	}
}
