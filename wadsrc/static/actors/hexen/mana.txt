// Blue mana ----------------------------------------------------------------

ACTOR Mana1 : Ammo
{
	Inventory.Amount 15
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 15
	Ammo.BackpackMaxAmount 200
	Radius 8
	Height 8
	+FLOATBOB
	Inventory.Icon "MAN1I0"
	Inventory.PickupMessage "$TXT_MANA_1"
	States
	{
	Spawn:
		MAN1 ABCDEFGHI 4 Bright
		Loop
	}
}

// Green mana ---------------------------------------------------------------

ACTOR Mana2 : Ammo
{
	Inventory.Amount 15
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 15
	Ammo.BackpackMaxAmount 200
	Radius 8
	Height 8
	+FLOATBOB
	Inventory.Icon "MAN2G0"
	Inventory.PickupMessage "$TXT_MANA_2"
	States
	{
	Spawn:
		MAN2 ABCDEFGHIJKLMNOP 4 Bright
		Loop
	}
}

// Combined mana ------------------------------------------------------------

ACTOR Mana3 : CustomInventory
{
	Radius 8
	Height 8
	+FLOATBOB
	Inventory.PickupMessage "$TXT_MANA_BOTH"
	States
	{
	Spawn:
		MAN3 ABCDEFGHIJKLMNOP 4 Bright
		Loop
	Pickup:
		TNT1 A 0 A_GiveInventory("Mana1", 20)
		TNT1 A 0 A_GiveInventory("Mana2", 20)
		Stop
	}
}

// Boost Mana Artifact Krater of Might ------------------------------------

ACTOR ArtiBoostMana : CustomInventory
{
	+FLOATBOB
	+COUNTITEM
	+INVENTORY.INVBAR
	+INVENTORY.PICKUPFLASH
	+INVENTORY.FANCYPICKUPSOUND
	Inventory.DefMaxAmount
	Inventory.Icon "ARTIBMAN"
	Inventory.PickupSound "misc/p_pkup"
	Inventory.PickupMessage "$TXT_ARTIBOOSTMANA"
	Tag "$TAG_ARTIBOOSTMANA"
	States
	{
	Spawn:
		BMAN A -1
		Stop
	Use:
		TNT1 A 0 A_GiveInventory("Mana1", 200)
		TNT1 A 0 A_GiveInventory("Mana2", 200)
		Stop
	}
}
